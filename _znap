#compdef znap

[[ -v _ZNAP_ARGS ]] ||
  znap &> /dev/null

local -a actions=()
local k; for k in ${(ko)_ZNAP_ARGS}; do
  actions+=( "$k\:'$_ZNAP_ARGS[$k]'" )
done

local ret=1
local -a context line state state_descr
local -A opt_args
_arguments -A '' ":action:(( $actions ))" '*:: :->next'
ret=$?

[[ $state == next ]] ||
  return ret

case $words[1] in
  clean)
    _arguments : '*:: :->dirs' ;;
  clone)
    _arguments : ':remote repository:_urls' ;;
  compdef)
    _arguments : ':command:_default' ;;
  compile)
    _arguments : '*:: :->files-functions' ;;
  eval)
    _arguments : ': :->repos-caches' ':command:_default' ;;
  function)
    _arguments : ':command:_default' '*::function:_functions' ;;
  help)
    _arguments : ":action:(( $actions ))" ;;
  ignore)
    _arguments : ':command:_default' ;;
  multi)
    _arguments : ':command:_default' ;;
  prompt)
    _arguments : ': :->repos' '*:: :->themes' ;;
  pull)
    _arguments : "*:: :->repos" ;;
  restart)
    _arguments : ':file:_files' ;;
  rm)
    _arguments : '*:: :->repos' ;;
  source)
    _arguments : ": :->repos" '*:: :->files' ;;
  status)
    _arguments : "*:: :->repos" ;;
esac
ret=$(( ret && ? ))

local -aU repos=( ~znap/*(/:t) )
case $state in
  dirs)
    _alternative 'directories:directory:_files -/'
    ;;
  files)
    local repo=~[$words[1]]
    [[ -d $repo ]] &&
      _arguments : "*::file:_files -W $repo -F '*.zwc'"
    ;;
  repos-caches)
    local -aU caches=( ~znap/*.zsh(#q.) )
    _alternative "repositories:repo:($repos)" "cache-files:cache:(${caches[@]:t:r})"
    ;;
  files-functions)
    _alternative "files:file:_files" "functions:function:_functions"
    ;;
  repos)
    _alternative "repositories:repo:($repos)"
    ;;
  themes)
    local repo=~[$words[1]]
    if [[ -d $repo ]]; then
      local -aU themes=( $repo/***.zsh-theme(#q.) )
      themes=( ${themes[@]:t:r} )
      _arguments : "::prompt theme:($themes)"
    fi
    ;;
esac

return $(( ret && ? ))
