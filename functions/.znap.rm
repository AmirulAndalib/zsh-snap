#!/bin/zsh
# rm repos and/or cache files
emulate -L zsh; setopt $_znap_opts

if (( $# < 1 )); then
  print -u2 'znap rm: not enough arguments'
  return 1
fi

local basedir=$(.znap.path) || return
local -aU files=(
  $basedir/$^@(#qNw/)
  $basedir/$^@.zsh(#qNw^/)
  $basedir/prompt_*$^@*_setup(#qNw^/)
)

(( $#files > 0 )) ||
  return 1
{
  local pwd=$PWD
  cd $basedir

  zmodload zsh/parameter
  if [[ -v commands[osascript] ]]; then
    print Moving $(eval "ls -d $files[@]:t") to Trash.
    local items=( '(POSIX file "'${^files:a}'")' )
    osascript -e 'tell application "Finder" to delete every item of {'${(j:, :)items}'}' >/dev/null
  else
    zmodload -F zsh/files b:zf_rm
    print rm $(eval "ls -d $files[@]:t")
    zf_rm -i $files[@]
  fi

} always {
  [[ -x $pwd ]] &&
    cd $pwd
}
