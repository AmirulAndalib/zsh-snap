#!/bin/zsh
# Delete repos and/or cache files
# args: <repo>|<cache name>...
zmodload -Fa zsh/files b:zf_rm
zmodload -Fa zsh/parameter p:commands

.znap.rm() {
  emulate -L zsh; setopt $_znap_opts

  if (( $# < 1 )); then
    print -u2 'znap rm: not enough arguments'
    .znap.help rm
    return 1
  fi
  if (( ${(M@)#@:#.##} > 0 )); then
    print -u2 znap rm: not a valid argument: ${(M@)@:#.##}
    .znap.help rm
    return 1
  fi

  local basedir=$(.znap.path) || return
  local -aU files=(
    $basedir/(prompt_*|)$^@(*_setup|.zsh|)(.zwc|)
  )

  (( $#files > 0 )) && {
    local pwd=$PWD
    cd -q $basedir

    if [[ -v commands[osascript] ]]; then
      print Moving $(eval "ls -d $files[@]:t") to Trash.
      local items=( '(POSIX file "'${^files:a}'")' )
      osascript -e \
        'tell application "Finder" to delete every item of {'${(j:, :)items}'}' >/dev/null
    else
      print rm $(eval "ls -d $files[@]:t")
      zf_rm -i $files[@]
    fi
  } always {
    [[ -x $pwd ]] &&
      cd -q $pwd
  }

  local receipts=${XDG_CONFIG_HOME:-$HOME/.config}/zsh/znap-repos
  local -aU repos=( "${(M@)${(f)"$(<$receipts)"}:#**/$1(|.git(|/))}" )
  if (( $#repos > 0 )); then
    print "Remove $1:t from $receipts${files:+, too}?"
    read -q "?[yn] " &&
      print -l "${(@)${(f)"$(<$receipts)"}:#**/$1(|.git(|/))}" >! $receipts
  fi
}

.znap.rm "$@"
