#!/bin/zsh
# validate dotfiles & safely restart Zsh
# args: --
emulate -LR zsh -o multios

local zdotdir=$ZDOTDIR
{
  unset ZDOTDIR
  print 'Validating...'
  local err="$(zsh --interactive --monitor --zle -c '' 2>&1 > /dev/null)"
  local -i ret=$?
  if (( ret )) || [[ -n $err ]]; then
    [[ -n $err ]] &&
        print -u2 "$err"
    print -nu2 'Validation failed'
    (( ret )) &&
        print -u2 " with exit status $ret"
    print -u2 '.'
    [[ -n $err ]] &&
        print -u2 ' Please fix the error(s) above.'
    print -u2 'Restart aborted.'
    (( ret )) ||
        (( ret = sysexits[(i)DATAERR] + 63 ))
    return ret
  else
    print 'Restarting...'
    exec zsh
  fi
} always {
  ZDOTDIR=$zdotdir
}
