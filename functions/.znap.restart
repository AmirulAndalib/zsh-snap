#!/bin/zsh
# validate dotfiles & safely restart Zsh
# args: --
emulate -LR zsh -o multios

local zdotdir=$ZDOTDIR
{
  unset ZDOTDIR
  print 'Validating dotfiles...'
  local err="$(zsh --interactive --monitor --zle -c '' 2>&1 > /dev/null)"
  local -i ret=$?
  if (( ret )) || [[ -n $err ]]; then
    [[ -n $err ]] &&
        print -ru2 -- "$err"
    print -nu2 'Validation failed'
    (( ret )) &&
        print -nu2 " with exit status $ret"
    [[ -n $err ]] &&
        print -nu2 '. Please fix the error(s) above'
    print -u2 '.'
    print -u2 'Restart aborted.'
    (( ret )) ||
        (( ret = sysexits[(i)DATAERR] + 63 ))
    return ret
  else
    print 'Restarting Zsh...'
    exec zsh
  fi
} always {
  ZDOTDIR=$zdotdir
}
