#!/bin/zsh
# instantly init prompt theme from repo
# args: [<repo>] <theme>
zmodload -Fa zsh/parameter p:functions
zmodload -Fa zsh/terminfo p:terminfo
autoload -Uz add-zle-hook-widget add-zsh-hook

.znap.prompt.setup() {
  emulate -L zsh; setopt $_znap_opts

  local setup=prompt_${theme}_setup
  if ! autoload +X -Uz $setup 2>/dev/null; then
    unfunction $setup

    local basedir=$(.znap.path) || return
    local -aU matches=( $basedir/{$plugin,*$plugin*}(-/) )
    local repo=$matches[1]

    if [[ -z $repo ]]; then
      print -u2 "znap prompt: no such theme '$*' in $basedir"
      .znap.help prompt
      return 1
    fi

    local file=( $repo/***/$setup(-.) )
    fpath=( $file:h $fpath[@] )
    if ! autoload +X -Uz $setup 2>/dev/null; then
      unfunction $setup

      local -a theme_file=( $repo/***/${~theme:-*}.zsh-theme(-.Y1) )
      print -r - .znap.source $repo:t ${theme_file[1]#$repo/} >! $basedir/$setup
      fpath=( $basedir $fpath[@] )
    fi
  fi
}

.znap.prompt() {
  if (( $# < 1 )); then
    print -u2 'znap prompt: not enough arguments'
    .znap.help prompt
    return 1
  fi

  local plugin=$1
  if [[ $plugin == */* ]]; then
    plugin=${${plugin##*/}%.git}
    [[ -d $(.znap.path)/$plugin ]] ||
      .znap.clone $1
  fi

  local theme=${2:-$plugin}
  .znap.prompt.setup "$@" || return

  setopt prompt{bang,cr,percent,subst} NO_promptsp
  autoload -Uz promptinit
  promptinit
  prompt $theme || return

  local precmd=prompt_${theme}_precmd
  [[ -n $functions[$precmd] ]] &&
    $precmd

  .znap.prompt.echoti sc     # save cursor position
  .znap.prompt.echoti civis  # invisible cursor

  [[ -o promptcr ]] &&
    print -n $'\r'

  :
  print -nP "$PS1"
  .znap.prompt.echoti cnorm  # normal, visible cursor

  add-zsh-hook precmd .znap.prompt.precmd
}

.znap.prompt.precmd() {
  add-zsh-hook -d precmd .znap.prompt.precmd

  .znap.prompt.echoti civis  # invisible cursor
  .znap.prompt.echoti rc     # restore cursor position
  .znap.prompt.echoti invis  # invisible text

  add-zle-hook-widget line-init .znap.prompt.line-init

  return 0
}

.znap.prompt.line-init() {
  add-zle-hook-widget -d line-init .znap.prompt.line-init

  zle .reset-prompt
  .znap.prompt.echoti cnorm  # normal, visible cursor
  zle -R

  return 0
}

.znap.prompt.echoti() {
  [[ -v terminfo[$1] ]] &&
    echoti $1
}

.znap.prompt "$@"
