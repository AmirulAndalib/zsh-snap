#!/bin/zsh
# source plugin or repo submodules & scripts

if (( $# < 1 )); then
  print -u2 'znap source: not enough arguments'
  return 1
fi

local repo=$(.znap.path $1) || return
shift
local -aU files=( $repo ); (( $# > 0 )) &&
  files=( $repo/$^@ )

local -aU src
local -i ret
local -a words ext=( '(|.*).zsh*' '.sh' '' )
local f; for f in $files; do
  () {
    emulate -L zsh; setopt $_znap_opts
    if [[ -d $f ]]; then
      src=( $f/(#i){*(${(~j:|:)${=${f:t}//[-_]/ }:#zsh})*,init,*}${^~ext}~*.zwc(-.rY1) )
    elif [[ -r $f ]]; then
      src=( $f )
    else
      src=( $f${^~ext}~*.zwc(-.rY1) )
    fi
   }
  . $src[1]; (( ret += ? ))
done
return ret
