#!/bin/zsh
# source plugin or repo submodules & scripts
# args: <repo> [<dir>|<file>...]

if (( $# < 1 )); then
  print -u2 'znap source: not enough arguments'
  .znap.help source
  return 64
fi

local plugin=$1
if [[ $plugin == */* ]]; then
  plugin=${${plugin##*/}%.git}
  () {
    setopt localoptions NO_nomatch
    [[ -d ~[$plugin] ]] ||
      .znap.clone $1
  }
fi

shift
local repo=~[$plugin]
local -aU files=( $repo ); (( $# > 0 )) &&
  files=( $repo/$^@ )

local -aU src
local -i ret
local -a words ext=( '(|.*).zsh*' '.sh' '' )
local f; for f in $files; do
  () {
    emulate -L zsh; setopt $_znap_opts

    if [[ -d $f ]]; then
      src=( $f/(#i){*(${(~j:|:)${=${f:t}//[-_]/ }:#zsh})*,init,*}${^~ext}~*.zwc(-.rY1) )
    elif [[ -r $f ]]; then
      src=( $f )
    else
      src=( $f${^~ext}~*.zwc(-.rY1) )
    fi
  }
  . $src[1]; (( ret = (ret || ?) ))
done
return ret
