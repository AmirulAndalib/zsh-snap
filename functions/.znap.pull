#!/bin/zsh
# update repos in parallel
# args: [ <repo> ... ]
zmodload -Fa zsh/files b:zf_rm

.znap.pull() {
  emulate -L zsh; setopt $_znap_opts

  local -a repos=( ~znap/$^@(#q/) ); (( $#repos > 0 )) ||
    repos=( ~znap/*(/) )

  {
    local pwd=$PWD
    cd -q -- ~znap
    .znap.multi '.znap.pull.task '${(i)^repos}
    return
  } always {
    [[ -x $pwd ]] &&
        cd -q -- $pwd
    [[ -v _comp_dumpfile && -f $_comp_dumpfile ]] &&
        zf_rm -f $_comp_dumpfile
    .znap.compile "$repos[@]"
  }
}

.znap.pull.task() {
  emulate -L zsh; setopt $_znap_opts

  .znap.ignore $1:t '*.zwc'  # Add `*.zwc` to repo's local ignore list.
  local behind err
  if behind=$( git -C $1 rev-list --right-only --count @..@{u} 2>&1 ) && (( behind )); then
    err="$(
      git -C $1 pull -q --autostash --rebase --recurse-submodules -j$( ulimit -n ) 2>&1 > /dev/null
    )" ||
        print "$err"
    .znap.status $1:t
    local cache_dir=$XDG_CACHE_HOME/zsh-snap
    zf_rm -f $cache_dir/fpath/_$1:t $cache_dir/eval/${1:t}.zsh $_comp_dumpfile
  fi
}

.znap.pull "$@"
