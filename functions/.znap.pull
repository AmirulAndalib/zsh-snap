#!/bin/zsh
# git pull repos in parallel
# args: [ <repo> ... ]
zmodload -Fa zsh/files b:zf_rm

.znap.pull() {
  emulate -L zsh; setopt $_znap_opts

  local -a repos=( ~znap/$^@(#q/) ); (( $#repos > 0 )) ||
    repos=( ~znap/*(/) )

  {
    local -i ret=1
    local pwd=$PWD
    cd -q ~znap

    .znap.multi '.znap.pull.task '${(i)^repos}
    ret=$?
    .znap.compile ~znap/*(-/)

    [[ -v _comp_dumpfile && -f $_comp_dumpfile ]] &&
      zf_rm -f $_comp_dumpfile

  } always {
    [[ -x $pwd ]] &&
      cd -q $pwd
  }
  return ret
}

.znap.pull.task() {
  emulate -L zsh; setopt $_znap_opts

  print -n "${$(eval 'ls -d '$1:t)%/}: "
  .znap.ignore $1 '*.zwc'  # Add `*.zwc` to repo's local ignore list.
  print $(
    git -C $1 pull --autostash --rebase --recurse-submodules -j$(ulimit -n) 2>&1
  )
  zf_rm -f $1.zsh $1.zsh.zwc
}

.znap.pull "$@"
