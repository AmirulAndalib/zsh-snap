#!/bin/zsh
# git pull repos in parallel
# args: [<repo>...]

.znap.pull() {
  emulate -L zsh; setopt $_znap_opts

  local basedir=$(.znap.path) || return
  local -a repos=( $basedir/$^@(#q/) ); (( $#repos > 0 )) ||
    repos=( $basedir/*(/) )

  {
    local -i ret=1
    local pwd=$PWD
    cd -q $basedir

    .znap.clean
    .znap.multi '.znap.pull.task '${(i)^repos}; ret=$?
    .znap.compile
  } always {
    [[ -x $pwd ]] &&
      cd -q $pwd
  }
  return ret
}

.znap.pull.task() {
  emulate -L zsh; setopt $_znap_opts

  .znap.clean $1/***(-/)
  local -i ret
  print ${$(eval 'ls -d '$1:t)%/}: $(
    git -C $1 pull --rebase --recurse-submodules -j$(ulimit -n) 2>&1; ret=$?
  )
  .znap.compile $1/***(-.)
  return ret
}

.znap.pull "$@"
