#!/bin/zsh
# remove repos & symlinked executables
# args: <repo> ...
emulate -L zsh; setopt $_znap_opts
zmodload -F zsh/files b:zf_rm

if (( $# < 1 )); then
  print -u2 'znap uninstall: not enough arguments'
  .znap.help uninstall
  return $(( sysexits[(i)USAGE] + 63 ))
fi

local -a comp exe
local -i ret=0
local repo
for repo in ~znap/${^@:t}; do
  if [[ -d $repo ]]; then
    comp=( $repo/**/_*(:t) )
    exe=( $repo/{,bin/}*(-*:t) )
    exe=( ~/.local/bin/$^exe(N) )
    zf_rm -frs $repo $exe[@] \
          ${XDG_DATA_HOME:-~/.local/share}/zsh/site-functions/$^comp(N) ||
        ret=1
    unhash $exe[@]:t
    print -P %F{12}${repo:t}%f uninstalled.
  else
    print -Pu2 %F{12}${repo:t} %F{1}not found.%f
    (( ret = sysexits[(i)NOINPUT] + 63 ))
  fi
done
hash -f
unfunction compdef compinit 2>/dev/null
autoload -Uz compinit
[[ -f $_comp_dumpfile ]] &&
    zf_rm -f $_comp_dumpfile
compinit -C -d $_comp_dumpfile
compinit() {:}
return ret
