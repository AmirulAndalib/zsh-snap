#!/bin/zsh
# remove executables & completion functions
# args: <repo> ...
emulate -L zsh; setopt $_znap_opts
zmodload -F zsh/files b:zf_rm

if (( $# < 1 )); then
  print -u2 'znap uninstall: not enough arguments'
  .znap.help uninstall
  return $(( sysexits[(i)USAGE] + 63 ))
fi

local -a abs comp exe file
local -i ret=0
local repo
for repo in ~znap/${^@:t}; do
  if [[ -d $repo ]]; then
    print 'Removing:'
    file=( ~/.local/bin/* ${XDG_DATA_HOME:-~/.local/share}/zsh/site-functions/* )
    while (( $#file ));do
      abs=( $file[1]:A )
      if [[ $abs == $repo/* ]]; then
        print -r -- ${(D)file[1]}
        zf_rm -frs $file[1]
        [[ -x $abs && ! -d $abs ]] &&
            unhash $file[1]:t
      fi
      shift file
    done
    zf_rm -frs $repo
    print -P "%F{12}${repo:t}%f uninstalled."
  else
    print -Pu2 "%F{12}${repo:t} %F{1}not found.%f"
    (( ret = sysexits[(i)NOINPUT] + 63 ))
  fi
done
hash -f
..znap.compinit
return ret
