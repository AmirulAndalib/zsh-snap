#!/bin/zsh
local repo
case $1 in
  n)
    () {
      emulate -L zsh; setopt extendedglob
      repo=~znap/$2
      if [[ $2 == */* ]]; then
        repo=~znap/${${2##*/}%.git}
        [[ -d $repo ]] ||
          .znap.clone $2
      fi
      if [[ -d $repo ]]; then
        reply=( $repo )
        return 0
      fi
      return 66
    } "$@"
    ;;
  d)
    () {
      emulate -L zsh; setopt extendedglob
      if [[ $2 == ~znap/* ]]; then
        repo=${${2#~znap/}:h1}
        reply=( $repo ${#:-~znap/$repo} )
        return 0
      fi
      return 66
    } "$@"
    ;;
  c)
    local expl tag='named-directories' group='named directory'
    _tags $tag
    _tags && _requested $tag expl $group &&
      compadd "$expl[@]" -I ']' -Q - ~znap/*(on-/:t)
    return
    ;;
esac
