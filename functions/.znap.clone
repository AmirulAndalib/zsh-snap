#!/bin/zsh
# git clone remotes in parallel to plugins dir
emulate -L zsh; setopt $_znap_opts
zmodload -F zsh/files b:zf_mkdir

local dir=${XDG_CONFIG_HOME:-$HOME/.config}/zsh/; [[ -d $dir ]] ||
  zf_mkdir -p $dir
local file=$dir/znap-repos
local -aU lines=(); [[ -f $file ]] &&
  lines=( "${(f)"$(<$file)"}" )
local -aU repos=( $@ ); (( $#repos > 0 )) ||
  repos=( $lines[@] )

local basedir=$(.znap.path) || return

if (( $#repos < 1 )); then
  print -u2 "znap clone: no repositories in $basedir or $file"
  return 1
fi

local -i ret=1
.znap.multi 'git -C $basedir clone --recurse-submodules -j$(ulimit -n) --depth=1 \
              --shallow-submodules '$^repos
ret=$?

lines+=( $repos[@] )
print -l ${(i)lines} >! $file

.znap.clean $basedir/***(N/)
.znap.compile $basedir/***(N.)
.znap.clean
.znap.compile

return ret
