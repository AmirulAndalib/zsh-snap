#!/bin/zsh
zmodload -Fa zsh/files b:zf_mkdir b:zf_rm
autoload -Uz add-zsh-hook

if zstyle -T :znap: auto-compile; then
  () {
    setopt localoptions NO_warncreateglobal
    zmodload -Fa zsh/parameter p:funcstack
  }

  source . () {
    builtin $0 "$@"; local -i ret=$?
    .znap.compile "$1:A" ${(M@)funcstack[@]:#*/*}
    return ret
  }
  add-zsh-hook preexec :znap:auto-compile
fi

add-zsh-hook zsh_directory_name :znap:dirname

export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
[[ ! -d $XDG_CACHE_HOME/zsh ]] &&
  zf_mkdir -pm 0700 $XDG_CACHE_HOME/zsh

typeset -gH _comp_dumpfile=${_comp_dumpfile:-$XDG_CACHE_HOME/zsh/compdump}
[[ -f $_comp_dumpfile && ${${:-${ZDOTDIR:-$HOME}/.zshrc}:A} -nt $_comp_dumpfile ]] &&
  zf_rm -f $_comp_dumpfile

zstyle -s :completion: cache-path _ ||
  zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/compcache"
zstyle -s ':completion:*' completer _ ||
  zstyle ':completion:*' completer _expand _complete _ignored

.znap.function bindkey 'zmodload zsh/complist'
if [[ ! -v _comp_setup ]]; then
  .znap.function compdef ':znap:compinit'
  add-zsh-hook precmd :znap:compinit
fi
.znap.function _bash_complete compgen complete '
  autoload -Uz bashcompinit
  bashcompinit
  bashcompinit() { : }
'

unfunction $0
