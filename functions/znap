#!/bin/zsh
zmodload -F zsh/zutil b:zparseopts

local -A __znap__opts_pre=() __znap__opts_post=()
local -a reply=()
local REPLY= __znap__cmd= __znap__func=

zparseopts -D -A __znap__opts_pre - h -help

if ! (( $# )); then
  .znap.help
  return
fi

__znap__cmd=$1
shift

case $__znap__cmd in
  ( cd | ls )
    print -u2 'znap: try `'"$__znap__cmd ~${${1:+[$1]}:-znap}"'` instead'
    return $(( sysexits[(i)USAGE] + 63 ))
  ;;
  ( path )
    print -u2 'znap: try `path+=( ~'"${${1:+[$1]}:-znap}"' )` instead'
    return $(( sysexits[(i)USAGE] + 63 ))
  ;;
  ( rm )
    print -u2 'znap: try `rm ~'"${${1:+[$1]}:-znap}"'` instead'
    return $(( sysexits[(i)USAGE] + 63 ))
  ;;
esac

zparseopts -D -A __znap__opts_post h -help
__znap__func=.znap.$__znap__cmd
if [[ -v functions[$__znap__func] ]]; then
  if (( ${#__znap__opts_pre} || ${#__znap__opts_post} )); then
    .znap.help $__znap__cmd
  else
    $__znap__func $@
  fi
else
  print -u2 "znap: unknown command: $__znap__cmd"
  .znap.help
  return $(( sysexits[(i)USAGE] + 63 ))
fi
