#!/bin/zsh
# DEPRECATED
# args: <function> <command>
zmodload -F zsh/files b:zf_mkdir

private _P__name=$1 _P__generator=$2
shift 2

private _P__cache_dir=$XDG_CACHE_HOME/zsh-snap/fpath
zf_mkdir -pm 0700 $_P__cache_dir
fpath=( $_P__cache_dir $fpath[@] )

private _P__cache_file=$_P__cache_dir/$_P__name
if [[ -r $_P__cache_file ]]; then
  local line=
  IFS='' read -r line < $_P__cache_file

  [[ $line != '#compdef '* ]] &&
      return $(( sysexits[(i)DATAERR] + 63 ))

  private -a _P__cmds=( ${=line#'#compdef '} )
  private _P__c=
  for _P__c in $_P__cmds[@]; do
    if [[ ${${:-=$_P__c}:a} -nt $_P__cache_file ]]; then
      eval "$_P__generator" >| $_P__cache_file ||
          return
      break
    fi
  done
else
  eval "$_P__generator" > $_P__cache_file ||
      return
fi
autoload -Uz $_P__cache_file

[[ -v _comp_setup ]] &&
    compdef $_P__name $_P__cmds[@]

true
