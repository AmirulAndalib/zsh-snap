#!/bin/zsh
# show git status of each repo
# args: [<repo>...]

.znap.status() {
  emulate -L zsh; setopt $_znap_opts

  local -a repos=( ~znap/$^@(#q/) ); (( $#repos > 0 )) ||
    repos=( ~znap/*(/) )

  {
    local -i ret=1
    local pwd=$PWD
    cd -q ~znap

    .znap.multi '.znap.status.task '${(i)^repos}
    ret=$?
  } always {
    [[ -x $pwd ]] &&
      cd -q $pwd
  }
  return ret
}

.znap.status.task() {
  emulate -L zsh; setopt $_znap_opts

  local file; for file in $1/${GIT_DIR:-.git}/***/info/exclude(.); do
    local -aU lines
    [[ -r $file ]] &&
      lines=( ${(f)"$(<$file)"} )
    lines+=( '*.zwc' )
    print -r "${(F)lines}" >! $file 2>/dev/null
  done

  git -C $1 fetch --recurse-submodules -j$(ulimit -n) &>/dev/null

  local repo=${$(eval "ls -d $1:t")%/}

  local log="$(git -C $1 log -n1 --decorate --oneline)"
  local rev
  if [[ $log ==  *'tag: '* ]]; then
    local -a chunks=( ${(s.tag: .)log} )
    rev="${chunks[1]##*[ (]}${chunks[2]%%[,)]*}"
  fi

  lines=( "${(f)$(git -C $1 status -sb)}" )
  local branch=${${lines[1]#'## '}%...*}
  local ahead_behind=${${(M)lines[1]% \[*\]}# }
  local -aU changes=( ${(0)${${(F)lines[2,-1]}// [[:print:]]#($'\n'|)}//$'m\C-['/$'m\0\C-['} )
  local MATCH MBEGIN MEND
  changes=( ${(i@)changes[@]//(#m)('??'|'!!')/$MATCH[1]} )

  [[ -z $ahead_behind && -z $changes ]] &&
      ahead_behind=$'\e[32mâœ“\e[39m'

  print -r $repo $rev ${branch:#*(main|master)*} $ahead_behind $changes
}

.znap.status "$@"
