#!/bin/zsh

# Resolve ~[$1]
..znap.dirname.n() {
  emulate -L zsh; setopt extendedglob

  [[ -z $1 ]] &&
      return 1

  local gitdir=
  ..znap.repos-dir

  private dir=$gitdir/$1
  if [[ $1 == */* ]]; then
    dir=$gitdir/${${1##*/}%.git}
    [[ -d $dir ]] ||
        .znap.clone $1
  fi

  [[ -d $dir ]] ||
      return 1

  reply=( $dir )
  return 0
}

# ~Abbreviate $1
..znap.dirname.d() {
  emulate -L zsh; setopt extendedglob

  local gitdir=
  ..znap.repos-dir

  if [[ $1 == $gitdir/* && -d $1/.git ]]; then
    # private name=${${1#$gitdir/}:h1}  <- Zsh 5.8+
    private name=${${1#$gitdir/}%%/*}
    reply=( "$name" ${#:-$HOMEznap/$name} )
    return 0
  fi
  return 1
}

# Complete ~[$PREFIX$SUFFIX
..znap.dirname.c() {
  private _P__tag=named-directories
  _tags $_P__tag

  local -a expl=()

  local gitdir=
  ..znap.repos-dir

  _tags &&
      _requested $_P__tag expl 'named directory' &&
          compadd "$expl[@]" -I ']' -Q - $gitdir/*/.git(on-/:h:t)
}

..znap.dirname() {
  ..znap.dirname.$1 "$2"
}

..znap.dirname "$@"
