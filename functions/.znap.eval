#!/bin/zsh
# run command, then cache & eval output
# args: <repo>|<name> <command>

.znap.eval() {
  if (( $# < 2 )); then
    print -u2 'znap eval: not enough arguments'
    .znap.help eval
    return 64
  fi

  local repo=~znap/$1 cache pwd=$PWD
  local cache=$repo.zsh
  shift
  local cmd="$@"
  local header="#${(q)cmd}"
  local -i ret=1
  if () {
    emulate -L zsh; setopt $_znap_opts
    local line
    [[ -r $cache ]] &&
      IFS='' read -r line < $cache
    [[ $line != $header ||
      -d $repo && -f $repo/.git/index && $repo/.git/index -nt $cache ]]
  } "$@"; then
    print -r "$header" >! $cache
    print -r "$(eval $cmd)" >>! $cache
  fi


  {
    [[ -d $repo ]] &&
      cd -q $repo
    . $cache; ret=$?
  } always {
    [[ -x $pwd ]] &&
      cd -q $pwd
  }
  return ret
}

.znap.eval "$@"
