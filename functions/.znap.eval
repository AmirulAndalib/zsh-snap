#!/bin/zsh
# run command, then cache & eval output
# args: <repo>|<name> <command>

.znap.eval() {
  if (( $# < 2 )); then
    print -u2 'znap eval: not enough arguments'
    .znap.help eval
    return 64
  fi

  local repo=$(.znap.path)/$1 || return
  shift
  local cmd="$@"
  local cache=$repo.zsh header="#${(q)cmd}"
  local -i ret=1
  {
    local pwd=$PWD
    [[ -d $repo ]] &&
      cd -q $repo

      local line
      [[ -r $cache ]] &&
        IFS='' read -r line < $cache
      if [[ $line != $header ]] || [[ -f $repo/.git/index && $repo/.git/index -nt $cache ]]; then
        print -r "$header" >! $cache
        print -r "$(eval $cmd)" >>! $cache
      fi
      . $cache; ret=$?
  } always {
    [[ -x $pwd ]] &&
      cd -q $pwd
  }
  return ret
}

.znap.eval "$@"
